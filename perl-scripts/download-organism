#!/usr/bin/perl -w
############################################################
#
# $Id: download-organism,v 1.4 2009/11/02 05:35:44 jvanheld Exp $
#
############################################################

## use strict;

=pod

=head1 NAME

download-organism

=head1 VERSION

$program_version

=head1 DESCRIPTION

Download organism(s) from the main RSAT server in order to add support
on a local machine.

Organisms can be loaded one by one (option -org), or by taxon (option
-taxon).

=head1 AUTHORS

Jacques.van.Helden@ulb.ac.be

=head1 CATEGORY

Data management

=head1 REQUIREMENTS

This program relies on a Web aspirator to download genomes from the
remote server to the local machine. By default, we use the freeware
program I<wget>.

=head1 USAGE

Download one or several organisms by species identifiers.

 download-organism -org Species_id_1 -org Species_id_2 ...


Download all the organisms belonging to a given taxon.

 download-organism -taxon taxon

=head1 OUTPUT

By default, the downloaded organisms are stored in the $RSAT genome
repository ($RSAT/public_html/data/genomes).

=head1 SEE ALSO

=head2 suported-organisms

The program I<supported-organism> can be used to obtain the list of
supported organisms on remote RSAT server.

 supported-organisms -server

The query to the remote server can also be restricted to a given
taxon.

 supported-organisms -server -taxon Fungi


=head2 install-organism

The program I<install-organism> performs all the formatting and
calibration tasks for importing genomes from the reference databases
(NCBI, EMBL) to RSAT. 

This differs from I<download-organism>, which transfers the
RSAT-formatted genomes from a RSAT server.

If a genome is available on the RSAT server, it is recommended to use
download-genomes in order to obtain it immediately in the RSAT format,
rather than install-genomes.

=head1 WISH LIST

=over

=item B<-web_aspirator>

Select the program for downloading the genome data. Default:
wget. Supported: curl.

=cut


BEGIN {
  if ($0 =~ /([^(\/)]+)$/) {
    push (@INC, "$`lib/");
  }
}
require "RSA.lib";
require RSAT::util;


################################################################
## Main package
package main;
{

  ################################################################
  ## Initialise parameters
  my $start_time = &AlphaDate();
  $program_version = do { my @r = (q$Revision: 1.4 $ =~ /\d+/g); sprintf "%d."."%02d" x $#r, @r };

  $verbose = 0;
  $dry = 0; ## Print the commands without executing them
  $die_on_error = 1;

  ## Remote server from which genomes should be downloaded
  $RSAT_SERVER="rsat.ulb.ac.be/rsat";

  ## List of organisms to install
  @organisms = ();

  ## List of taxa to install
  @taxa = ();

  ## Output directory
  $output_dir = $ENV{RSAT}."/public_html/data/genomes/";
  $main::out = STDOUT; ## Output stream for verbosity

  ################################################################
  ## Read argument values
  &ReadArguments();

  ################################################################
  ## Check that the web aspirator is working
  $web_aspirator = "wget";
  $which_aspirator = `which $web_aspirator`;
  &RSAT::message::Debug($which_aspirator) if ($main::verbose >= 3);
  unless ($which_aspirator) {
    &RSAT::error::FatalError("The program ".$web_aspirator." is not found in your path. This program is required for downloading data from the server. ");
  }

  ################################################################
  ## Check argument values
  if ((scalar(@organisms) == 0) && (scalar(@taxa) == 0)) {
    &FatalError("You must specify at leastone organism (-org) or taxon (-taxon)."); 
  }

  ## Output directory
  &RSAT::message::Info("Download directory", $output_dir) if ($main::verbose >= 1);
  &RSAT::util::CheckOutDir($output_dir);
#  chdir($output_dir);

  ################################################################
  ## Print verbose
  &Verbose() if ($main::verbose);

  ################################################################
  ## Obtain the lists of organisms for selected taxa
  foreach my $taxon (@taxa) {
    &RSAT::message::TimeWarn("Getting list of organisms from RSAT server for taxon", $taxon);
    my $orgs_for_taxa = `supported-organisms -server -return ID -taxon $taxon | grep -v '^#'`;
    chomp ($orgs_for_taxa);
    @orgs_for_taxa = split(/\s+/, $orgs_for_taxa);
    push @organisms, @orgs_for_taxa;
  }

  &RSAT::message::Info("Downloading", scalar(@organisms), "organisms");
  &RSAT::message::Info(join("\n\t", "Organisms: ", @organisms));


  ################################################################
  ## Get the update dates + source from the server (in order to restrict the
  ## number of queries, get dates for all organisms and store them for
  ## the selected organisms only)
  my @all_organisms = `supported-organisms -v 0 -server -return ID,last_update,source | grep -v '^#'`;
  my %last_update;
  my %source;
  foreach my $org_line (@all_organisms) {
    chomp($org_line);
    my ($org, $date, $source) = split /\t/, $org_line;
    $last_update{$org} = $date;
    $source{$org} = $source || '<NA>';
  }

  ################################################################
  ## Download the genomes
  foreach my $org (@organisms) {
    ## download one genome
    &RSAT::message::TimeWarn("Downloading genome", $org, "from the RSAT server", $RSAT_SERVER) if ($main::verbose >= 1);
    my $cmd = "wget --reject 'index.html*' ";
    if ($main::verbose <= 1) {
	$cmd .= "-q"; ## quiet mode for wget
    } else {
	$cmd .= "-nv"; ## low-verbosity mode for wget (the default mode is VERY verbosy)
    }
    $cmd .= " --passive-ftp";
    $cmd .= " --no-parent";
    $cmd .= " --recursive";
    $cmd .= " --timestamping";
    $cmd .= " --relative";
    $cmd .= " --dont-remove-listing";
    $cmd .= " --convert-links";
    $cmd .= " -P ".$output_dir;
    $cmd .= " -nH  --cut-dirs=3";
    $cmd .= " http://".${RSAT_SERVER}."/data/genomes/".$org."/";
    &doit($cmd, $dry, $die_on_error, $verbose, 0, 0);

    ## update the organism in the RSAT configuration tables
    &RSAT::message::TimeWarn("Updating organism",$org) if ($main::verbose >= 1);
    $cmd = "install-organism -v 1 -task config,phylogeny -org ".$org;
    $cmd .= " -date '".$last_update{$org}."'";
    $cmd .= " -source '".$source{$org}."'";
    &doit($cmd, $dry, $die_on_error, $verbose, 0, 0);
  }


  ################################################################
  ## Finish verbose
  if ($main::verbose >= 1) {
    my $done_time = &AlphaDate();
    print $main::out "; Job started $start_time\n";
    print $main::out "; Job done    $done_time\n";
  }

    exit(0);
}

################################################################
################### SUBROUTINE DEFINITION ######################
################################################################


################################################################
## Display full help message 
sub PrintHelp {
    system "pod2text -c $0";
    exit()
}

################################################################
## Display short help message
sub PrintOptions {
    &PrintHelp();
}

################################################################
## Read arguments 
sub ReadArguments {
    my $arg;
    my @arguments = @ARGV; ## create a copy to shift, because we need ARGV to report command line in &Verbose()
    while (scalar(@arguments) >= 1) {
      $arg = shift (@arguments);
	## Verbosity
=pod

=head1 OPTIONS

=over 4

=item B<-v #>

Level of verbosity (detail in the warning messages during execution)

=cut
	if ($arg eq "-v") {
	    if (&IsNatural($arguments[0])) {
		$main::verbose = shift(@arguments);
	    } else {
		$main::verbose = 1;
	    }

	    ## Help message
=pod

=item B<-h>

Display full help message

=cut
	} elsif ($arg eq "-h") {
	    &PrintHelp();

	    ## List of options
=pod

=item B<-help>

Same as -h

=cut
	} elsif ($arg eq "-help") {
	    &PrintOptions();

	    ## Selected organism(s)
=pod

=item B<-org organism>

ID of the organism to be downloaded. This ID is generally the full
name of the organism, where spaces and weird characters have been
replaced by an underscore (_).

Ex: download-organism -v 1 -org Escherichia_coli_K12

The option -org can be used iteratively on teh command line to specify
multiple organisms.

 download-organism -v 1 -org Escherichia_coli_K12 \
    -org Mycoplasma_genitalium \
    -org Saccharomyces_cerevisiae

=cut

} elsif ($arg eq "-org") {
	  push @organisms,  shift(@arguments);

	    ## Selected taxon (or taxa)
=pod

=item B<-taxon taxon>

Name of a taxon for wchich all organisms will be downloaded. 

The option -taxon can be used iteratively on the command line to
specify multiple taxa.

 download-organism -v 1 -taxon Enterobacteriales \
    -taxon Saccharomycetales

=cut

} elsif ($arg eq "-taxon") {
	  push @taxa,  shift(@arguments);

	    ## Output directory
=pod

=item	B<-dir output directory>


Directory for exporting the genome(s) of the selected organism(s).  

By default, genomes are exported in the standar RSAT genome folder
($RSAT/public_html/data/genomes/). This requires write permissions on
this folder. If you don't have those writing permissions, specifying
an alternative directory may be convenient for obtaining the data
files, but the downloaded genoems will not be supported on the local
machine unless the configuration file is adapted (and this anyways
requires the writing permission in the RSAT directory).

=cut
	} elsif ($arg eq "-dir") {
	    $main::output_dir = shift(@arguments);


	    ## Dry run
=pod

=item B<-dry>

Dry run: print the commands but do not execute them (just for
testing).

=cut
	} elsif ($arg eq "-dry") {
	    $main::dry = 1;;

	} else {
	    &FatalError(join("\t", "Invalid option", $arg));

	}
    }

=pod

=back

=cut

}

################################################################
## Verbose message
sub Verbose {
  print $main::out "; download-organism ";
  &PrintArguments($main::out);
  printf $main::out "; %-22s\t%s\n", "Program version", $program_version;
  if (defined(%main::infile)) {
    print $main::out "; Input files\n";
    while (my ($key,$value) = each %main::infile) {
      printf $main::out ";\t%-13s\t%s\n", $key, $value;
    }
  }
  if (defined(%main::outfile)) {
    print $main::out "; Output files\n";
    while (my ($key,$value) = each %main::outfile) {
      printf $main::out ";\t%-13s\t%s\n", $key, $value;
    }
  }
}


__END__
